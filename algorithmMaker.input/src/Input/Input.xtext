grammar algorithmMaker.Input with org.eclipse.xtext.common.Terminals
//TODO: Sugar: math signs, comparators
//TODO: Sugar: implies "->, <-, <->" : relies on inline declarations?
//TODO: Nested atomics??
generate input "http://www.Input"

//NOTE: Any changes to the formatting should be reflected with changes here.
Input:
	'Given' given=Problem ';' ((task=("Find" | "Count") goal=Problem) | (task="Test" '(' goal=ProblemNoVars ')'))
	(';' "Theorems:" theorems+=Theorem (';' theorems+=Theorem)* ';'?)?;

ProblemNoVars returns Problem:
	property=ORing;

Problem:
	(vars+=Declaration) (',' vars+=Declaration)* 'st' property=ORing;

Declaration:
	(type=Type '(' varName=ID ')') | varName=ID;

Type:
	name=ID ('<' templateType=Type '>')?;

ORing returns Property:
	ANDing ({ORing.left=current} '|' right=ANDing)*;

ANDing returns Property:
	Primary ({ANDing.left=current} '&' right=Primary)*;

Primary returns Property:
	Atomic | Quantifier | ('(' ORing ')') | BooleanLiteral;

Quantifier:
	quantifier=('forall' | 'exists') '(' subject=Problem ':' predicate=ORing ')';

Atomic:
	Function=ID ('(' (args+=ID (',' args+=ID)*)? ')')?;

BooleanLiteral:
	value=("TRUE" | "FALSE");

Theorem:
	Requirement=ORing ":-" Result=ORing ',' Cost=INT ',' Description=STRING (',' PseudoCode=STRING)?;